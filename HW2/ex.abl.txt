MODULE      octcntr
TITLE      'Octal Up Counter'


" deccntr       DEVICE  'MACH4032'


" Description:  This implements a 3-Digit Octal Up Counter for EE/CS 10a.


" Revision History:
" 04/16/07   Glen George  Initial Revision
" 04/18/07   Glen George  Fixed high digit, was using CountEn1 instead of
"                         CountEn2
" 04/19/07   Glen George  Fixed a couple more minor typos in the equations
" 10/13/07   Glen George  Changed CountUp pin to be called CountDown (its
"                         active high meaning)
" 10/13/07   Glen George  Fixed typo in Q6 and Q10 that caused errors in the
"                         count sequence
" 10/13/07   Glen George  Updated comments
" 01/12/18   Glen George  Updated pinout to match new PCB (v2.0)
" 01/08/20   Glen George  Updated comments
" 01/04/21   Glen George  Updated comments



" Pins

" Inputs

Mode		pin 41;			"input   Mode (not used)
CountDown	pin 14;			"input   Up/Down input (not used)


" Counter Outputs

Q11		pin 31   ISTYPE 'REG';  "output octal digit 2, bit 3
Q10		pin 35   ISTYPE 'REG';  "output octal digit 2, bit 2
Q9		pin 36   ISTYPE 'REG';  "output octal digit 2, bit 1
Q8		pin 37   ISTYPE 'REG';  "output octal digit 2, bit 0
Q7		pin 24   ISTYPE 'REG';  "output octal digit 1, bit 3
Q6		pin 25   ISTYPE 'REG';  "output octal digit 1, bit 2
Q5		pin 26   ISTYPE 'REG';  "output octal digit 1, bit 1
Q4		pin 29   ISTYPE 'REG';  "output octal digit 1, bit 0
Q3		pin 18   ISTYPE 'REG';  "output octal digit 0, bit 3
Q2		pin 19   ISTYPE 'REG';  "output octal digit 0, bit 2
Q1		pin 20   ISTYPE 'REG';  "output octal digit 0, bit 1
Q0		pin 21   ISTYPE 'REG';  "output octal digit 0, bit 0


" Clock Input

Clock           pin 39;			"input   clock


" Unconnected Pins

"IOA2		pin 42                   input/output
"IOA3		pin 43                   input/output
"IOA4		pin 44                   input/output
"IOA5		pin  2                   input/output
"IOA6		pin  3                   input/output
"IOA7		pin  4                   input/output
"IOA8		pin  7                   input/output
"IOA9		pin  8                   input/output
"IOA10		pin  9                   input/output
"IOA12		pin 13                   input/output
"IOA14		pin 15                   input/output
"IOA15		pin 16                   input/output
"IOB4		pin 22                   input/output
"IOB9		pin 30                   input/output

"IOGOE0		pin 40                   input/output/output enable
"IOGOE1		pin 38                   input/output/output enable
"ICLK2		pin 17                   input/clock


" Programming Pins (not available for use in the design)

"TCK		pin 10                   pgm     programming interface TCK
"TDI		pin  1                   pgm     programming interface TDI
"TDO		pin 32                   pgm     programming interface TDO
"TMS		pin 23                   pgm     programming interface TMS


" Power Pins

"GND		pin  5                   supply  power ground
"GND		pin 12                   supply  power ground
"GND		pin 27                   supply  power ground
"GND		pin 34                   supply  power ground
"VCC   		pin 11                   supply  power Vcc
"VCC   		pin 33                   supply  power Vcc
"VCCIO 		pin 28                   supply  power Vcc I/O
"VCCIO 		pin  6                   supply  power Vcc I/O


" Intermediate Terms

CountEn1	node;		" enable out of digit 0 into digit 1 (7 -> 0)
CountEn2	node;		" enable out of digit 1 into digit 2 (7 -> 0)



EQUATIONS


" clocks for all of the octal outputs

Q11.CLK  =  Clock;                      " use the global clock pin
Q10.CLK  =  Clock;                      " use the global clock pin
Q9.CLK   =  Clock;                      " use the global clock pin
Q8.CLK   =  Clock;                      " use the global clock pin
Q7.CLK   =  Clock;                      " use the global clock pin
Q6.CLK   =  Clock;                      " use the global clock pin
Q5.CLK   =  Clock;                      " use the global clock pin
Q4.CLK   =  Clock;                      " use the global clock pin
Q3.CLK   =  Clock;                      " use the global clock pin
Q2.CLK   =  Clock;                      " use the global clock pin
Q1.CLK   =  Clock;                      " use the global clock pin
Q0.CLK   =  Clock;                      " use the global clock pin


" octal counter digits
"    each digit is the same
"    CountEn[i] is the count being at 7 with the lower digits enabling the
"       count for this digit, in other words, when the next digit should count

" low order digit, a divide-by-8 binary counter

Q0  :=  !Q0;
Q1  :=  (!Q1 & Q0) # (Q1 & !Q0);
Q2  :=  (!Q2 & Q1 & Q0) # (Q2 & !Q1) # (Q2 & !Q0);
Q3  :=  0;                      " high bit is never used

CountEn1  =  (Q2 & Q1 & Q0);    " enable next digit to count when at 7


" middle digit, a divide-by-8 binary counter
"    hold value when counting is not enabled

Q4  :=  (CountEn1 & !Q4) # (!CountEn1 & Q4);
Q5  :=  (CountEn1 & !Q5 & Q4) # (!CountEn1 & Q5) # (Q5 & !Q4);
Q6  :=  (CountEn1 & !Q6 & Q5 & Q4) # (!CountEn1 & Q6) #
        (Q6 & !Q5) # (Q6 & !Q4);
Q7  :=  0;                             " high bit is never used

CountEn2  =  (CountEn1 & Q6 & Q5 & Q4);" enable next digit to count when at 7
                                       "    and counting is enabled


" high order digit, a divide-by-8 binary counter
"    hold value when counting is not enabled

Q8   :=  (CountEn2 & !Q8) # (!CountEn2 & Q8);
Q9   :=  (CountEn2 & !Q9 & Q8) # (!CountEn2 & Q9) # (Q9 & !Q8);
Q10  :=  (CountEn2 & !Q10 & Q9 & Q8) # (!CountEn2 & Q10) #
         (Q10 & !Q9) # (Q10 & !Q8);
Q11  :=  0;                      " high bit is never used


END  octcntr